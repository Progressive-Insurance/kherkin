plugins {
    id 'com.android.library'
    id 'maven-publish'
    id 'org.jreleaser'
}

def getVersionName = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--tags', 'HEAD'
            standardOutput = stdout
        }
        def describe = stdout.toString().trim()
        if (describe.contains("-")) {
            describe += "-SNAPSHOT"
        }
        return describe
    } catch (Exception ignored) {
        return "1.0.0"
    }
}

android {
    compileSdk 34
    version getVersionName()

    publishing {
        singleVariant("release") {
            withSourcesJar()
            withJavadocJar()
        }
    }

    defaultConfig {
        minSdk 24
        targetSdk 34
        versionName getVersionName()
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = '17'
    }
}

tasks.register('prepareDirs') {
    mustRunAfter clean
    doLast {
        mkdir "build/jreleaser"
    }
}

afterEvaluate {
    tasks.withType(PublishToMavenRepository).configureEach {
        dependsOn(tasks.prepareDirs)
    }

    publishing {
        publications {
            publishLibrary(MavenPublication) {
                groupId 'com.progressive.kherkin'
                artifactId ARTIFACT_ID

                afterEvaluate {
                    from components.release
                }

                pom {
                    name = ARTIFACT_ID
                    description = DESCRIPTION
                    url = 'https://github.com/Progressive-Insurance/kherkin'
                    inceptionYear = '2024'

                    licenses {
                        license {
                            name = 'MIT License'
                            url = 'http://www.opensource.org/licenses/mit-license.php'
                        }
                    }

                    developers {
                        developer {
                            name = 'Matthew Rockwell'
                            email = 'smugleafdev@gmail.com'
                        }
                    }

                    scm {
                        connection = 'scm:git:https://github.com/Progressive-Insurance/kherkin.git'
                        developerConnection = 'scm:git:ssh://github.com/Progressive-Insurance/kherkin.git'
                        url = 'http://github.com/Progressive-Insurance/kherkin'
                    }
                }
            }
        }

        repositories {
            maven {
                url = rootProject.layout.buildDirectory.dir("staging-deploy").get().asFile.absolutePath.toString()
            }
        }
    }
}

jreleaser {
    gitRootSearch = true
    signing {
        active = 'ALWAYS'
        armored = true
    }
    deploy {
        maven {
            mavenCentral {
                sonatype {
                    active = 'RELEASE'
                    url = 'https://central.sonatype.com/api/v1/publisher'
                    stagingRepository(rootProject.layout.buildDirectory.dir("staging-deploy").get().asFile.absolutePath.toString())
                    verifyPom = false
                }
            }
            nexus2 {
                'maven-central' {
                    active = 'SNAPSHOT'
                    snapshotSupported = true
                    url = 'https://s01.oss.sonatype.org/service/local'
                    snapshotUrl = 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
                    stagingRepository(rootProject.layout.buildDirectory.dir("staging-deploy").get().asFile.absolutePath.toString())
                    verifyPom = false
                }
            }
        }
    }

    release {
        github {
            repoOwner = 'Progressive-Insurance'
            name = 'kherkin'
            branch = 'main'
            token = System.getenv('JRELEASER_GITHUB_TOKEN')
            skipTag = true
            tagName = getVersionName()
            changelog {
                enabled = false
            }
            releaseNotes {
                enabled = true
            }
        }
    }
}